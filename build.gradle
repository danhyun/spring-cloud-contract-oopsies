buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${project.hasProperty('contractsVersion') ? project.getProperty('contractsVersion') : '2.0.2.RELEASE'}"
  }
}

plugins {
  id 'org.springframework.boot' version '2.0.5.RELEASE'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

apply plugin: 'spring-cloud-contract'

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${project.hasProperty('contractsVersion') ? project.getProperty('contractsVersion') : '2.0.2.RELEASE'}"
  }
}

repositories {
  jcenter()
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  testCompile 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

contracts {
  baseClassForTests = 'hello.MvcTest'
  basePackageForTests 'hello.verifier'
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

task printYamls {
  doLast {
    def yamls = file('src/test/resources/contracts/rest/foo')

    yamls.listFiles().each {
      println "Found yaml: '$it.name'\n\n${it.text}"
    }
  }
}

task printGeneratedTests(dependsOn: generateContractTests) {
  doLast {
    def generated = file('build/generated-test-sources/contracts/hello/verifier/rest')

    generated.listFiles().each {
      println "Found generated test: '$it.name'\n\n${it.text}"
    }
  }
}

tasks.compileTestJava.dependsOn(printYamls, printGeneratedTests)